// Imports
var _CollectorData = import Games/Farming/Data/CollectorData

// Properties
var _collectionRate = null
var _lootTable = null

var level = null
var value = null
var capacity = null

func _SetData(level)
  ?level >= _CollectorData.minLevel & level <= _CollectorData.maxLevel
    this.level = level
    this.value = 0

    this.capacity = _CollectorData.capacity[level]
    this._collectionRate = _CollectorData.collectionRate[level]
    this._lootTable = _CollectorData.lootTable[level]
  :
    // TODO: Panic
    >c#red,AHHHHHH
    loc.Pause()

func Init(level)
  this._SetData(level)

func Upgrade()
  // TODO: Possibly delete bool return value. Might not be used.
  ?this.level < _CollectorData.maxLevel
    this._SetData(this.level + 1)
    return true
  :
    return false

func Collect()
  // TODO: Loot table implementation
  var amount = math.FloorToInt(this.value) // Only collect whole number amounts
  this.value = this.value - amount
  return amount

func Update()
  this.value = this.value + (this._collectionRate / 30.0)
  // TODO: Replace with clamp utility function
  ?this.value < 0
    this.value = 0
  :?this.value > this.capacity
    this.value = this.capacity