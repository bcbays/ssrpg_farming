// TODO: Figure out how to decouple state and scene. Or remove state altogether. Possible scenes can manage their own states.

// Imports
var _GameState = import Games/Farming/Enums/GameState
var _PlayerData = import Games/Farming/PlayerData

// Variables
var initialized = false
var _state = _GameState.None

var _CollectorComponents = []

var _entranceScene = null
var _mainHubScene = null
var _farmScene = null
var _outlookScene = null
var _workshopScene = null

// Functions
func Init()
  ?this.initialized
    return

  // Scenes
  this._entranceScene = import Games/Farming/Scenes/Entrance
  this._entranceScene.Init()
  this._mainHubScene = import Games/Farming/Scenes/MainHub
  this._mainHubScene.Init()
  this._farmScene = import Games/Farming/Scenes/Farm
  this._farmScene.Init()
  this._outlookScene = import Games/Farming/Scenes/Outlook
  this._outlookScene.Init()
  this._workshopScene = import Games/Farming/Scenes/Workshop
  this._workshopScene.Init()

  // Disable HUD Elements
  disable abilities
  disable banner
  disable hud
  disable loadout input
  disable loadout print
  disable player

  // State
  this._state = _GameState.Entrance
  this.initialized = true

func SetState(state)
  this._OnStateChangeFrom(this._state)
  this._OnStateChangeTo(state)
  this._state = state

func _GetScene(state)
  // FIXME: This feels like it could be done in a better way
  ?state = _GameState.Entrance
    return this._entranceScene
  :?state = _GameState.MainHub
    return this._mainHubScene
  :?state = _GameState.Farm
    return this._farmScene
  :?state = _GameState.Outlook
    return this._outlookScene
  :?state = _GameState.Workshop
    return this._workshopScene
  // TODO: Panic or handle null
  return null

func _OnStateChangeFrom(state)
  var scene = this._GetScene(state)
  scene.OnStateChangeFrom()

func _OnStateChangeTo(state)
  var scene = this._GetScene(state)
  scene.OnStateChangeTo()

func Update()
  ?this._state = _GameState.None
    // TODO: Make this panic or something
    >#red,GameState.None
    loc.Pause()
  :
    var scene = this._GetScene(this._state)
    scene.Update()
    scene.Draw()
  _PlayerData.Update()

  // TODO: Extract save interval to config
  ?time % 30 = 0
    _PlayerData.SaveResources()

  // ?_PlayerData.collectors.Count() > 0
  //   >`1,1,@Collectors@
  //   for i = 0..(_PlayerData.collectors.Count() - 1)
  //     >`1,@2 + i@,@i@: @_PlayerData.collectors[i].value@