var _GameState = import Games/Farming/Enums/GameState

var initialized = false
var _state = _GameState.None

var _HUDComponent = null

var _entranceScene = null
var _mainHubScene = null
var _farmScene = null
var _workshopScene = null

func Init()
  ?initialized
    return
  
  // Components
  _HUDComponent = import Games/Farming/Components/HUD
  _HUDComponent.Init()

  // Scenes
  _entranceScene = import Games/Farming/Scenes/Entrance
  _entranceScene.Init()
  _mainHubScene = import Games/Farming/Scenes/MainHub
  _mainHubScene.Init()
  _farmScene = import Games/Farming/Scenes/Farm
  _farmScene.Init()
  _workshopScene = import Games/Farming/Scenes/Workshop
  _workshopScene.Init()

  // Disable HUD Elements
  disable abilities
  disable banner
  disable hud
  disable loadout input
  disable loadout print
  disable player
  
  _HUDComponent.DrawOnTop()

  // State
  _state = _GameState.Entrance
  initialized = true

func SetState(state)
  OnStateChangeFrom(_state)
  OnStateChangeTo(state)
  _state = state

func OnStateChangeFrom(state) 
  ?state = _GameState.Entrance
    _entranceScene.OnStateChangeFrom()
  :?state = _GameState.MainHub
    _mainHubScene.OnStateChangeFrom()
  :?state = _GameState.Farm
    _farmScene.OnStateChangeFrom()
  :?state = _GameState.Workshop
    _workshopScene.OnStateChangeFrom()

func OnStateChangeTo(state) 
  ?state = _GameState.Entrance
    _entranceScene.OnStateChangeTo()
  :?state = _GameState.MainHub
    _mainHubScene.OnStateChangeTo()
  :?state = _GameState.Farm
    _farmScene.OnStateChangeTo()
  :?state = _GameState.Workshop
    _workshopScene.OnStateChangeTo()

func Update()
  ?_state = _GameState.None
    // TODO: Make this panic or something
    >#red,GameState.None
    loc.Pause()
  :?_state = _GameState.Entrance
    _entranceScene.Update()
    _entranceScene.Draw()
  :?_state = _GameState.MainHub
    _mainHubScene.Update()
    _mainHubScene.Draw()
    _HUDComponent.Update()
  :?_state = _GameState.Farm
    _farmScene.Update()
    _farmScene.Draw()
    _HUDComponent.Update()
  :?_state = _GameState.Workshop
    _workshopScene.Update()
    _workshopScene.Draw()
    _HUDComponent.Update()