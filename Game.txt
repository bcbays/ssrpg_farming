var _GameState = import Games/Farming/Enums/GameState

var initialized = false
var _state = _GameState.None

var _entranceComponent = null
var _mainHubComponent = null
var _farmComponent = null

func Init()
  ?initialized
    return
  // Components
  _entranceComponent = new Games/Farming/Scenes/Entrance
  _entranceComponent.Init()
  _mainHubComponent = new Games/Farming/Scenes/MainHub
  _mainHubComponent.Init()
  _farmComponent = new Games/Farming/Scenes/Farm
  _farmComponent.Init()
  // Disable HUD Elements
  disable abilities
  disable banner
  disable hud
  disable loadout input
  disable loadout print
  disable player
  // State
  _state = _GameState.Entrance
  initialized = true

func SetState(state)
  OnStateChangeFrom(_state)
  OnStateChangeTo(state)
  _state = state

func OnStateChangeFrom(state) 
  ?state = _GameState.Entrance
    _entranceComponent.Hide()
  :?state = _GameState.MainHub
    _mainHubComponent.Hide()
  :?state = _GameState.Farm
    _farmComponent.Hide()

func OnStateChangeTo(state) 
  ?state = _GameState.Entrance
    _entranceComponent.Show()
  :?state = _GameState.MainHub
    _mainHubComponent.Show()
  :?state = _GameState.Farm
    _farmComponent.Show()

func Update()
  ?_state = _GameState.None
    // TODO: Make this panic or something
    >#red,GameState.None
    loc.Pause()
  ?_state = _GameState.Entrance
    _entranceComponent.Update()
    _entranceComponent.Draw()
    // >test
    return
  ?_state = _GameState.MainHub
    _mainHubComponent.Update()
    _mainHubComponent.Draw()
    return
  ?_state = _GameState.Farm
    _farmComponent.Update()
    _farmComponent.Draw()
    return